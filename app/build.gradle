apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def oktaProperties = new Properties()
rootProject.file("okta.properties").withInputStream { oktaProperties.load(it) }

android {
    compileSdkVersion build_versions.compile_sdk

    defaultConfig {
        applicationId "com.okta.idx.android"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName '1.0'

        buildConfigField "String", 'ISSUER', "\"${oktaProperties.getProperty('issuer')}\""
        buildConfigField "String", 'CLIENT_ID', "\"${oktaProperties.getProperty('clientId')}\""
        buildConfigField "String", 'REDIRECT_URI', "\"${oktaProperties.getProperty('signInRedirectUri')}\""

        manifestPlaceholders = [
                "oktaIdxRedirectScheme": parseScheme(oktaProperties.getProperty('signInRedirectUri'))
        ]

        testInstrumentationRunner 'io.cucumber.android.runner.CucumberAndroidJUnitRunner'
        testInstrumentationRunnerArguments = [
                cucumberUseAndroidJUnitRunner: findProperty("cucumberUseAndroidJUnitRunner") ?: 'true',
        ]
    }

    sourceSets {
        androidTest {
            java.srcDirs += ['src/sharedTest/java']
        }
        test {
            java.srcDirs += ['src/sharedTest/java']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    testOptions {
        animationsDisabled true

        execution = "ANDROIDX_TEST_ORCHESTRATOR"
    }
}

dependencies {
    coreLibraryDesugaring deps.desugar_libs

    implementation deps.kotlin.stdlib
    implementation deps.core_ktx
    implementation deps.app_compat
    implementation deps.constraint_layout
    implementation deps.navigation.fragment_ktx
    implementation deps.navigation.ui_ktx
    implementation deps.lifecycle.viewmodel_ktx
    implementation deps.lifecycle.livedata_ktx
    implementation deps.lifecycle.runtime_ktx
    implementation deps.lifecycle.java8
    implementation deps.material
    implementation deps.coroutines.android
    implementation deps.okhttp.core
    implementation deps.okhttp.tls
    implementation deps.okhttp.mock_web_server
    implementation deps.junit // Mock Web Server depends on junit :(
    implementation deps.timber
    implementation deps.okta.idx

    implementation deps.jackson_databind

    testImplementation deps.junit
    testImplementation deps.okhttp.mock_web_server
    testImplementation deps.okhttp.tls
    testImplementation deps.truth

    androidTestUtil deps.androidx_test.orchestrator

    androidTestImplementation deps.jackson_yaml
    androidTestImplementation deps.okhttp.tls
    androidTestImplementation deps.junit
    androidTestImplementation deps.androidx_test.core
    androidTestImplementation deps.androidx_test.ext_junit
    androidTestImplementation deps.androidx_test.navigation
    androidTestImplementation deps.androidx_test.runner
    androidTestImplementation deps.androidx_test.rules
    androidTestImplementation deps.androidx_test.uiautomator
    androidTestImplementation deps.coroutines.test
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.espresso.contrib
    androidTestImplementation deps.espresso.intents
    androidTestImplementation deps.truth

    androidTestImplementation deps.cucumber.android
    androidTestImplementation deps.cucumber.pico_container
    androidTestImplementation(deps.okta.management_sdk) {
        exclude group: 'org.bouncycastle'
    }
}

static def parseScheme(String uri) {
    def index = uri.indexOf(':/')
    if (index == -1) {
        throw new IllegalStateException("Scheme is not in a valid format.")
    }
    return uri.substring(0, index)
}
