apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def oktaProperties = new Properties()
rootProject.file("okta.properties").withInputStream { oktaProperties.load(it) }

android {
    compileSdkVersion build_versions.compile_sdk

    defaultConfig {
        applicationId "sample.okta.oidc.android.sessionToken"
        minSdkVersion 23
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName '1.0'

        buildConfigField "String", 'ISSUER', "\"${oktaProperties.getProperty('issuer')}\""
        buildConfigField "String", 'CLIENT_ID', "\"${oktaProperties.getProperty('clientId')}\""
        buildConfigField "String", 'SIGN_IN_REDIRECT_URI', "\"${oktaProperties.getProperty('signInRedirectUri')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        pickFirst 'META-INF/okta/version.properties'
    }
}

dependencies {
    coreLibraryDesugaring deps.desugar_libs

    implementation project(':auth-foundation-bootstrap')
    implementation project(':oauth2')

    implementation 'com.okta.authn.sdk:okta-authn-sdk-api:2.0.7'
    runtimeOnly('com.okta.authn.sdk:okta-authn-sdk-impl:2.0.7') {
        exclude group: 'com.okta.sdk', module: 'okta-sdk-httpclient'
        exclude group: 'org.bouncycastle'
    }
    implementation 'com.okta.sdk:okta-sdk-okhttp:8.0.0'

    implementation deps.kotlin.stdlib
    implementation deps.core_ktx
    implementation deps.app_compat
    implementation deps.constraint_layout
    implementation deps.navigation.fragment_ktx
    implementation deps.navigation.ui_ktx
    implementation deps.lifecycle.viewmodel_ktx
    implementation deps.lifecycle.livedata_ktx
    implementation deps.lifecycle.runtime_ktx
    implementation deps.lifecycle.java8
    implementation deps.material
    implementation deps.coroutines.android
    implementation deps.okhttp.core
    implementation deps.okhttp.tls
    implementation deps.timber

    debugImplementation deps.leakcanary.android
}
