apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def oktaProperties = new Properties()
rootProject.file("okta.properties").withInputStream { oktaProperties.load(it) }

android {
    compileSdkVersion build_versions.compile_sdk

    defaultConfig {
        applicationId "sample.okta.oidc.android.sessionToken"
        minSdkVersion 23
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName '1.0'

        buildConfigField "String", 'ISSUER', "\"${oktaProperties.getProperty('issuer')}\""
        buildConfigField "String", 'CLIENT_ID', "\"${oktaProperties.getProperty('clientId')}\""
        buildConfigField "String", 'SIGN_IN_REDIRECT_URI', "\"${oktaProperties.getProperty('signInRedirectUri')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    packagingOptions {
        resources {
            excludes += 'META-INF/*'
            pickFirsts += ['META-INF/okta/version.properties']
        }
    }

    namespace 'sample.okta.android.sessiontoken'
}

dependencies {
    coreLibraryDesugaring libs.desugar.libs

    implementation project(':oauth2')

    implementation libs.okta.authn.api
    runtimeOnly(libs.okta.authn.impl) {
        exclude group: 'com.okta.sdk', module: 'okta-sdk-httpclient'
        exclude group: 'org.bouncycastle'
    }
    runtimeOnly(libs.jackson.databind)
    runtimeOnly(libs.snakeyaml)

    implementation libs.kotlin.stdlib
    implementation libs.core.ktx
    implementation libs.app.compat
    implementation libs.navigation.fragment.ktx
    implementation libs.navigation.ui.ktx
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.runtime.ktx
    implementation libs.lifecycle.java8
    implementation libs.material
    implementation libs.coroutines.android
    implementation libs.okhttp.core
    implementation libs.okhttp.tls
    implementation libs.okio.core
    implementation libs.okio.jvm
    implementation libs.timber

    debugImplementation libs.leakcanary.android
}
