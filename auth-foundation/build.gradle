apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.vanniktech.maven.publish.base'
apply plugin: 'binary-compatibility-validator'

android {
    compileSdkVersion build_versions.compile_sdk

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn", "-Xopt-in=com.okta.authfoundation.InternalAuthFoundationApi"]
    }
    buildFeatures {
        buildConfig = false
    }

    sourceSets {
        main {
            kotlin.srcDir("$buildDir/generated/sources/kotlinTemplates")
        }
    }
}

dependencies {
    coreLibraryDesugaring deps.core_library_desugaring

    api deps.kotlin.stdlib
    api deps.okhttp.core
    api deps.coroutines.core
    api deps.kotlin.serialization_json

    implementation deps.security_crypto

    testImplementation deps.androidx_test.core
    testImplementation deps.junit
    testImplementation deps.truth
    testImplementation deps.kotlin.test
    testImplementation deps.mockito.core
    testImplementation deps.mockito.kotlin
    testImplementation deps.robolectric
    testImplementation project(':test-helpers')

    androidTestImplementation deps.junit
    androidTestImplementation deps.truth
    androidTestImplementation deps.kotlin.test
    androidTestImplementation deps.androidx_test.core
    androidTestImplementation deps.androidx_test.ext_junit
    androidTestImplementation deps.androidx_test.runner
    androidTestImplementation deps.androidx_test.rules
    androidTestImplementation project(':test-helpers')
}

task copyKotlinTemplates(type: Copy) {
    from("src/main/kotlinTemplates")
    into("$buildDir/generated/sources/kotlinTemplates")
    expand(projectVersion: project.version)
}

tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile) {
    dependsOn(copyKotlinTemplates)
}
