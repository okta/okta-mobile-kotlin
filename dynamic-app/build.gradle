apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'

def localProperties = new Properties()
if (rootProject.file("local.properties").exists()) {
    rootProject.file("local.properties").withInputStream { localProperties.load(it) }
}

android {

    compileSdkVersion build_versions.compile_sdk

    defaultConfig {
        applicationId "com.okta.idx.android"
        minSdkVersion 23
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName '1.0'

        buildConfigField "String", 'ISSUER', "\"${localProperties.getProperty('issuer')}\""
        buildConfigField "String", 'CLIENT_ID', "\"${localProperties.getProperty('clientId')}\""
        buildConfigField "String", 'REDIRECT_URI', "\"${localProperties.getProperty('signInRedirectUri')}\""

        manifestPlaceholders = [
            "oktaIdxRedirectScheme": parseScheme(localProperties.getProperty('signInRedirectUri')),
            "oktaIdxEmailHost"     : localProperties.getProperty('emailRedirectHost'),
            "oktaIdxEmailPrefix"   : localProperties.getProperty('emailRedirectPrefix')
        ]

        testInstrumentationRunner 'io.cucumber.android.runner.CucumberAndroidJUnitRunner'
        testInstrumentationRunnerArguments = [
            cucumberUseAndroidJUnitRunner: findProperty("cucumberUseAndroidJUnitRunner") ?: 'false',
        ]
    }

    sourceSets {
        androidTest {
            java.srcDirs += ['src/sharedTest/java']
        }
        test {
            java.srcDirs += ['src/sharedTest/java']
        }
    }

    signingConfigs {
        create("debugCertFpt") {
            storeFile file(localProperties.getProperty('storeFile'))
            storePassword localProperties.getProperty('storePassword')
            keyAlias localProperties.getProperty('keyAlias')
            keyPassword localProperties.getProperty('keyPassword')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig = signingConfigs.getByName("debugCertFpt")
            minifyEnabled = false
            debuggable = true
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    packagingOptions {
        resources {
            excludes += 'META-INF/*'
        }
    }


    testOptions {
        animationsDisabled true

        execution = "ANDROIDX_TEST_ORCHESTRATOR"
    }
    namespace 'com.okta.idx.android.dynamic'
}

dependencies {
    coreLibraryDesugaring libs.desugar.libs
    implementation project(':okta-idx-kotlin')


    implementation libs.kotlin.stdlib
    implementation libs.core.ktx
    implementation libs.app.compat
    implementation libs.androidx.constraintlayout
    implementation libs.navigation.fragment.ktx
    implementation libs.navigation.ui.ktx
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.runtime.ktx
    implementation libs.lifecycle.java8
    implementation libs.material
    implementation libs.coroutines.android
    implementation libs.okhttp.core
    implementation libs.okhttp.tls
    implementation libs.okio.core
    implementation libs.okio.jvm
    implementation libs.timber
    implementation libs.androidx.credentials.credentials
    implementation libs.androidx.credentials.play.services.auth

    implementation libs.jackson.databind

    testImplementation libs.junit
    testImplementation libs.okhttp.mock.web.server
    testImplementation libs.okhttp.tls
    testImplementation libs.okio.core
    testImplementation libs.okio.jvm
    testImplementation libs.truth
    testImplementation project(':test-utils')

    androidTestUtil libs.androidx.test.orchestrator

    androidTestImplementation libs.jackson.yaml
    androidTestImplementation libs.okhttp.tls
    androidTestImplementation libs.okio.core
    androidTestImplementation libs.okio.jvm
    androidTestImplementation libs.junit
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.navigation
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.uiautomator
    androidTestImplementation libs.coroutines.test
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.espresso.contrib
    androidTestImplementation libs.espresso.intents
    androidTestImplementation libs.jsoup
    androidTestImplementation libs.truth
    androidTestImplementation project(':test-utils')

    androidTestImplementation libs.cucumber.android
    androidTestImplementation(libs.okta.management.sdk) {
        exclude group: 'org.bouncycastle'
    }
}

static def parseScheme(String uri) {
    def index = uri.indexOf(':/')
    if (index == -1) {
        throw new IllegalStateException("Scheme is not in a valid format.")
    }
    return uri.substring(0, index)
}
