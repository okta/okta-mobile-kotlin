apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'binary-compatibility-validator'
apply plugin: 'com.vanniktech.maven.publish.base'

def copyKotlinTemplates = tasks.register('copyKotlinTemplates', Copy) {
    from("src/main/kotlinTemplates")
    into("$buildDir/generated/sources/kotlinTemplates")
    expand(projectVersion: project.version)
}

android {
    compileSdkVersion build_versions.compile_sdk

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=com.okta.authfoundation.InternalAuthFoundationApi"]
    }

    buildFeatures {
        buildConfig = false
    }

    sourceSets {
        main {
            kotlin.srcDir("$buildDir/generated/sources/kotlinTemplates")
        }
    }
    namespace 'com.okta.idx.kotlin'
}

dependencies {
    coreLibraryDesugaring libs.desugar.libs
    api libs.kotlin.stdlib
    api libs.okhttp.core
    api libs.okio.core
    api libs.coroutines.android
    api libs.okta.auth.foundation

    implementation libs.androidx.datastore.preferences
    implementation libs.kotlin.serialization.json
    implementation libs.okio.jvm
    implementation libs.security.crypto

    testImplementation libs.androidx.test.ext.junit
    testImplementation libs.coroutines.test
    testImplementation libs.junit
    testImplementation libs.okhttp.mock.web.server
    testImplementation libs.okhttp.tls
    testImplementation libs.okio.core
    testImplementation libs.okio.jvm
    testImplementation libs.truth
    testImplementation libs.jackson.databind
    testImplementation libs.robolectric
    testImplementation libs.bcprov.jdk18on
    testImplementation project(':test-utils')
}

preBuild.dependsOn(copyKotlinTemplates)
