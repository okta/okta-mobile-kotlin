buildscript {
    apply from: 'versions.gradle'
    addRepos(repositories)
    forceVersions(configurations)

    dependencies {
        classpath deps.android_gradle_plugin
        classpath deps.dokka_gradle_plugin
        classpath deps.kotlin.binary_compatibility_plugin
        classpath deps.kotlin.plugin
        classpath deps.navigation.safe_args_plugin
        classpath deps.kotlin.serialization_plugin
        classpath deps.gradle_maven_publish
        classpath deps.spotless
    }
}

apply plugin: 'org.jetbrains.dokka'

allprojects {
    group = project.property('GROUP')
    version = project.property('VERSION_NAME')

    addRepos(repositories)
    forceVersions(configurations)
    configureSpotless(it)
    configurePublishing(it)
    configureDokka(it)
}

def configurePublishing(Project project) {
    project.plugins.withId('com.vanniktech.maven.publish.base') {
        project.apply plugin: 'signing'

        project.mavenPublishing {
            publishToMavenCentral("DEFAULT")
            pomFromGradleProperties()
        }

        if (project.hasProperty('signWithGpgCommand')) {
            project.afterEvaluate {
                project.signing {
                    useGpgCmd()
                    sign project.publishing.publications
                }
            }
        }

        if (project.hasProperty('signAllPublications')) {
            project.mavenPublishing {
                signAllPublications()
            }
        }

        if (project.plugins.hasPlugin('com.android.library')) {
            project.mavenPublishing.configure(new com.vanniktech.maven.publish.AndroidSingleVariantLibrary("release"))
        }
    }
}

def configureDokka(Project project) {
    project.tasks.withType(org.jetbrains.dokka.gradle.DokkaTaskPartial).configureEach {
        dokkaSourceSets {
            configureEach {
                jdkVersion.set(8)
                suppressInheritedMembers.set(true)

                perPackageOption {
                    matchingRegex.set(".*\\.internal.*")
                    suppress.set(true)
                }

                externalDocumentationLink {
                    url.set(new URL("https://okta.github.io/okta-mobile-kotlin/"))
                }
                externalDocumentationLink {
                    url.set(new URL("https://kotlin.github.io/kotlinx.serialization/"))
                    packageListUrl.set(new URL("https://kotlin.github.io/kotlinx.serialization/package-list"))
                }
            }
        }
    }
}

tasks.dokkaHtmlMultiModule.configure {
    outputDirectory.set(file("${rootDir}/docs"))
}
