import com.vanniktech.maven.publish.AndroidSingleVariantLibrary
import org.jetbrains.dokka.gradle.DokkaTaskPartial

buildscript {
    apply from: 'utils.gradle'
    addRepos(repositories)
    forceVersions(configurations)

    dependencies {
        classpath libs.android.gradle.plugin
        classpath libs.dokka.gradle.plugin
        classpath libs.gradle.maven.publish
        classpath libs.kotlin.binary.compatibility.plugin
        classpath libs.kotlin.plugin
        classpath libs.kotlin.serialization.plugin
        classpath libs.navigation.safe.args.plugin
        classpath libs.spotless
    }
}

plugins {
    id 'com.google.devtools.ksp' version '2.0.0-1.0.22' apply false
}

apply plugin: 'org.jetbrains.dokka'

allprojects {
    group = project.property('GROUP')
    version = project.property('VERSION_NAME')

    addRepos(repositories)
    forceVersions(configurations)
    configurePublishing(it)
    configureSpotless(it)
    configureDokka(it)
}

subprojects {
    tasks.register("allDeps", DependencyReportTask) {}
}

def configurePublishing(Project project) {
    project.plugins.withId('com.vanniktech.maven.publish.base') {
        project.apply plugin: 'signing'

        project.mavenPublishing {
            publishToMavenCentral("DEFAULT")
            pomFromGradleProperties()
        }

        if (project.hasProperty('signWithGpgCommand')) {
            project.afterEvaluate {
                project.signing {
                    useGpgCmd()
                    sign project.publishing.publications
                }
            }
        }

        if (project.hasProperty('signAllPublications')) {
            project.mavenPublishing {
                signAllPublications()
            }
        }

        if (project.plugins.hasPlugin('com.android.library')) {
            project.mavenPublishing.configure(new AndroidSingleVariantLibrary("release"))
        }
    }
}

def configureDokka(Project p) {
    p.tasks.withType(DokkaTaskPartial).configureEach {
        dokkaSourceSets {
            configureEach {
                jdkVersion.set(11)
                suppressInheritedMembers.set(true)

                perPackageOption {
                    matchingRegex.set(".*\\.internal.*")
                    suppress.set(true)
                }

                externalDocumentationLink {
                    url.set(new URL("https://square.github.io/okhttp/4.x/"))
                    packageListUrl.set(new URL("https://square.github.io/okhttp/4.x/okhttp/package-list"))
                }
                externalDocumentationLink {
                    url.set(new URL("https://kotlin.github.io/kotlinx.serialization/"))
                    packageListUrl.set(new URL("https://kotlin.github.io/kotlinx.serialization/package-list"))
                }
            }
        }
    }

    p.plugins.withId('org.jetbrains.dokka') {
        p.dependencies {
            dokkaPlugin(project(':suppress-internal-dokka-plugin'))
        }
    }
}

tasks.dokkaHtmlMultiModule.configure {
    outputDirectory.set(file("${rootDir}/docs"))
}
