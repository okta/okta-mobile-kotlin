buildscript {
    apply from: 'utils.gradle'
    addRepos(repositories)
    forceVersions(configurations)

    dependencies {
        classpath libs.android.gradle.plugin
        classpath libs.dokka.gradle.plugin
        classpath libs.kotlin.binary.compatibility.plugin
        classpath libs.kotlin.plugin
        classpath libs.navigation.safe.args.plugin
        classpath libs.kotlin.serialization.plugin
        classpath libs.gradle.maven.publish
        classpath libs.spotless
    }
}

apply plugin: 'org.jetbrains.dokka'

allprojects {
    group = project.property('GROUP')
    version = project.property('VERSION_NAME')

    addRepos(repositories)
    forceVersions(configurations)
    configureSpotless(it)
    configurePublishing(it)
    configureDokka(it)
}

subprojects {
    task allDeps(type: DependencyReportTask) {}
}

def configurePublishing(Project project) {
    project.plugins.withId('com.vanniktech.maven.publish.base') {
        project.apply plugin: 'signing'

        project.mavenPublishing {
            publishToMavenCentral("DEFAULT")
            pomFromGradleProperties()
        }

        if (project.hasProperty('signWithGpgCommand')) {
            project.afterEvaluate {
                project.signing {
                    useGpgCmd()
                    sign project.publishing.publications
                }
            }
        }

        if (project.hasProperty('signAllPublications')) {
            project.mavenPublishing {
                signAllPublications()
            }
        }

        if (project.plugins.hasPlugin('com.android.library')) {
            project.mavenPublishing.configure(new com.vanniktech.maven.publish.AndroidSingleVariantLibrary("release"))
        }
    }
}

def configureDokka(Project project) {
    project.tasks.withType(org.jetbrains.dokka.gradle.DokkaTaskPartial).configureEach {
        dokkaSourceSets {
            configureEach {
                jdkVersion.set(8)
                suppressInheritedMembers.set(true)

                perPackageOption {
                    matchingRegex.set(".*\\.internal.*")
                    suppress.set(true)
                }

                externalDocumentationLink {
                    url.set(new URL("https://okta.github.io/okta-mobile-kotlin/"))
                }
                externalDocumentationLink {
                    url.set(new URL("https://kotlin.github.io/kotlinx.serialization/"))
                    packageListUrl.set(new URL("https://kotlin.github.io/kotlinx.serialization/package-list"))
                }
            }
        }
    }
}

tasks.dokkaHtmlMultiModule.configure {
    outputDirectory.set(file("${rootDir}/docs"))
}
