buildscript {
    apply from: 'versions.gradle'
    addRepos(repositories)
    forceVersions(configurations)

    dependencies {
        classpath deps.android_gradle_plugin
        classpath deps.gradle_maven_publish
        classpath deps.kotlin.binary_compatibility_plugin
        classpath deps.kotlin.plugin
        classpath deps.kotlin.serialization_plugin
        classpath deps.navigation.safe_args_plugin
        classpath deps.spotless
    }
}

allprojects {
    group = project.property('GROUP')
    version = project.property('VERSION_NAME')

    addRepos(repositories)
    forceVersions(configurations)
    configurePublishing(it)
    configureSpotless(it)
}

def configurePublishing(Project project) {
    project.plugins.withId('com.vanniktech.maven.publish.base') {
        project.apply plugin: 'signing'

        project.mavenPublishing {
            publishToMavenCentral("DEFAULT")
            pomFromGradleProperties()
        }

        if (project.hasProperty('signWithGpgCommand')) {
            project.afterEvaluate {
                project.signing {
                    useGpgCmd()
                    sign project.publishing.publications
                }
            }
        }

        if (project.hasProperty('signAllPublications')) {
            project.mavenPublishing {
                signAllPublications()
            }
        }

        if (project.plugins.hasPlugin('com.android.library')) {
            project.mavenPublishing.configure(new com.vanniktech.maven.publish.AndroidSingleVariantLibrary("release"))
        }
    }
}
